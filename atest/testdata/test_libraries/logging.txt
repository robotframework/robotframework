*** Settings ***
Documentation   Tests for logging using stdout/stderr
Library         ExampleLibrary  WITH NAME  Py
Library         HtmlPrintLib.py
Library         ExampleJavaLibrary  WITH NAME  Java
Library         String

*** Test Cases ***
Logging From Python Library Using Stdout And Stderr
    Py.Print  Hello from Python Library!
    Py.Print  Hello to stderr from Python Library!  stderr
    Py.Print to stdout and stderr  Hello!!

Log Non-ASCII As Unicode From Python
    Py.Print  Hyvää päivää stdout!
    Py.Print  Hyvää päivää stderr!  stderr

Log Non-ASCII As Bytes From Python
    ${encoding} =    Evaluate    robot.utils.encoding.OUTPUT_ENCODING    robot
    ${bytes} =    Encode String To Bytes    Hyvää päivää!    ${encoding}
    Py.Print  ${bytes}
    Py.Print  ${bytes}  stderr

Logging HTML From Python
    Print One HTML Line
    Print Many HTML Lines
    Print HTML To Stderr

Logging From Java Library Using Stdout And Stderr
    [Setup]  Java.Print  Hello\nworld\n!!
    Java.Print  Hello from Java library!
    Java.Stderr  Hello Java stderr!!

Log Non-ASCII As Unicode From Java
    Java.Print  Hyvää päivää java stdout!
    Java.Stderr  Hyvää päivää java stderr!

Logging with Levels From Java
    Java.Print   This is debug  DEBUG
    Java.Print   First msg\n2nd line of1st msg\n*INFO* 2nd msg *INFO* Still 2nd
    Java.Print  *INFO*1st msg\n2nd line\n*WARN* Second msg\n*INVAL* Still 2nd\n*INFO*Now 3rd msg
    Java.Stderr  Warning to stderr  WARN

Logging HTML From Java
    Java.Print  <b>Hello, stdout!</b>  HTML
    Java.Stderr  <b>Hello, stderr!</b>  HTML
