*** Settings ***
Resource          resource.txt
Test Template     Elements should not be equal

*** Test Cases ***
Elements should be equal
    [Template]    Element should be equal to itself
    <tag/>
    <root><tag/></root>
    <tag a="1" b="2"/>
    <root>\n<tag>some\ntext</tag>tail text</root>

Different tag names
    <tag/>   <t채g/>    Different tag name: tag != t채g

Different attributes
    <tag a="1"/>   <tag a="2"/>    Different value for attribute 'a': 1 != 2
    <tag a="1" c="3"/>   <tag b="1"/>
    ...   Different attribute names: ['a', 'c'] != ['b']

Different texts
    <tag>some text</tag>   <tag>different</tag>  Different text: some text != different
    <tag>some text</tag>   <tag/>    Different text: some text !=${SPACE}

Different tail texts
    <root><tag/>tail</root>   <root><tag/>wrong</root>
    ...    Different tail text at 'root/tag': tail != wrong
    <root><tag/>tail</root>   <root><tag/></root>
    ...    Different tail text at 'root/tag': tail !=${SPACE}

Different number of children
    <root><tag/><tag/></root>    <root><tag/></root>   Different number of child elements: 2 != 1

Differences in children
    <root><tag/></root>    <root><different/></root>
    ...    Different tag name at 'root/tag': tag != different
    <a a="a"><b><c><d/></c></b></a>    <a a="a"><b><c><wrong/></c></b></a>
    ...    Different tag name at 'a/b/c/d': d != wrong
    <root><tag/></root>    <root><tag a="1"/></root>
    ...    Different attribute names at 'root/tag': [] != ['a']
    <a><b><c a="a" b="b"/></b></a>    <a><b><c a="채" b="b"></c></b></a>
    ...    Different value for attribute 'a' at 'a/b/c': a != 채
    <root><tag/></root>    <root><tag>text</tag></root>
    ...    Different text at 'root/tag': \ != text
    <root><tag><c1/><c2/></tag></root>    <root><tag/></root>
    ...    Different number of child elements at 'root/tag': 2 != 0

Normalize whitespace
    [Template]    NONE
    Elements should be equal    <p>\n\tThis \ \ \ text\n<i>spaces \ has</i> also \ in\ttail!\n</p>
    ...   <p>This text <i>spaces has</i> also in tail!</p>    normalize
    Elements should not be equal    <tag>\ntext \ here\n</tag>    <tag>\twrong \ here\t</tag>
    ...   Different text: text here != wrong here    normalize

*** Keywords ***
Element should be equal to itself
    [Arguments]    ${source}
    ${xml}=    Parse XML    ${source}
    Elements Should Be Equal    ${source}    ${xml}
    Elements Should Be Equal    ${xml}    ${source}

Elements should not be equal
    [Arguments]   ${source}    ${expected}    ${error}    ${normalize}=${FALSE}
    Run Keyword and Expect Error    ${error}
    ...    Elements Should Be Equal   ${source}    ${expected}    ${normalize}
