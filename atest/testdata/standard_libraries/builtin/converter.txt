*** Settings ***
Variables       numbers_to_convert.py

*** Test Cases ***
Convert To Integer
    [Documentation]  FAIL STARTS: 'MyObject' cannot be converted to an integer: ZeroDivisionError:
    Test Convert To Integer  1  ${1}
    Test Convert To Integer  -42  ${-42}
    ${googol} =  Convert To Integer  10000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
    Should Be True  ${googol} == 10**100
    Test Convert To Integer  ${OBJECT}  ${42}
    Convert To Integer  ${OBJECT_FAILING}

Convert To Integer With Java Objects
    [Documentation]  FAIL STARTS: 'foobar' cannot be converted to an integer: NumberFormatException:
    Test Convert To Integer  ${JAVA_STRING_INT}
    Test Convert To Integer  ${JAVA_INTEGER}
    Test Convert To Integer  ${JAVA_LONG}
    Test Convert To Integer  ${JAVA_SHORT}
    Test Convert To Integer  ${JAVA_FLOAT}
    Test Convert To Integer  ${JAVA_DOUBLE}
    Test Convert To Integer  ${JAVA_STRING_INVALID}  This fails!

Convert To Number
    [Documentation]  FAIL REGEXP: ^'MyObject' cannot be converted to a floating point number: (ZeroDivision|Attribute)Error: .*$
    Test Convert To Number  -10.000  ${-10}
    ${e} =  Convert To Number  2.7182818284590451
    Fail Unless  round(__import__('math').log(${e}), 8) == 1.0
    Test Convert To Number  ${OBJECT}  ${42.0}
    Convert To Number  ${OBJECT_FAILING}

Convert To Number With Java Objects
    [Documentation]  FAIL STARTS: 'foobar' cannot be converted to a floating point number: NumberFormatException:
    Test Convert To Number  ${JAVA_STRING_INT}  ${1.0}
    Test Convert To Number  ${JAVA_STRING_FLOAT}  ${1.1}
    Test Convert To Number  ${JAVA_INTEGER}  ${1.0}
    Test Convert To Number  ${JAVA_LONG}  ${1.0}
    Test Convert To Number  ${JAVA_SHORT}  ${1.0}
    Test Convert To Number  ${JAVA_FLOAT}  ${1.1}
    Test Convert To Number  ${JAVA_DOUBLE}  ${1.1}
    Test Convert To Number  ${JAVA_STRING_INVALID}  This fails!

Convert To String
    ${int42} =  Convert To Integer  42
    Should Not Be Equal  ${int42}  42
    ${str42} =  Convert To String  ${int42}
    Should Be Equal  ${str42}  42

Convert To Boolean
    ${True} =  Convert To Boolean  True
    ${False} =  Convert To Boolean  false
    Fail Unless  ${True} == True
    Fail Unless  ${False} == False
    ${one} =  Convert To Integer  1
    ${zero} =  Convert To Integer  0
    ${True} =  Convert To Boolean  ${one}
    ${False} =  Convert To Boolean  ${zero}
    Fail Unless  ${True} == True
    Fail Unless  ${False} == False

Create List
    ${list} =  Create List  hello  world
    Fail Unless  ${list} == ['hello','world']
    @{list} =  Create List  hello  world
    Should Be Equal  @{list}[0]  hello
    Should Be Equal  @{list}[1]  world
    ${one_item} =  Create List  one item
    Fail Unless  ${one_item} == ['one item']
    ${empty} =  Create List
    Fail Unless  ${empty} == [ ]
    ${int_one} =  Convert To Number  1
    ${mixed} =  Create List  one  ${int_one}
    Fail Unless  ${mixed} == ['one', 1]

*** Keywords ***
Test Convert To Integer
    [Arguments]  ${item}  ${exp}=${1}
    ${act} =  Convert To Integer  ${item}
    Should Be Equal  ${act}  ${exp}

Test Convert To Number
    [Arguments]  ${item}  ${exp}=${1.0}
    ${act} =  Convert To Number  ${item}
    Should Be True  round(${act}, 6) == ${exp}

