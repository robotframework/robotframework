*** Settings ***
Suite Setup     Run Tests  ${EMPTY}  test_libraries/dynamic_libraries_with_invalid_argspec.txt
Force Tags      regression  jybot  pybot
Resource        atest_resource.txt

*** Test Cases ***

Argspec consists of something else than strings
    Error message should be correct    0    argspec with other than strings
    ...    Calling dynamic method 'get_keyword_arguments' failed: Return value must be list of strings.

Argspec has varargs before positional arguments
    Error message should be correct    3    varargs before positional args
    ...    Positional argument after varargs.

Argspec has varargs before named arguments
    Error message should be correct    2    varargs before named args
    ...    Positional argument after varargs.

Argspec has named arguments before positional
    Error message should be correct    1    named args before positional
    ...     Non-default argument after default arguments.


*** Keywords ***

Error message should be correct
    [Arguments]    ${index}    ${kw}    ${msg}
    Check Test Case    ${TESTNAME}
    Check Log Message    ${ERRORS[${index}]}    Adding keyword '${kw}' to library 'DynamicLibrary' failed: ${msg}    WARN
