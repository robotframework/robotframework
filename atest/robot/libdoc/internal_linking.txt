*** Settings ***
Force Tags       regression    pybot    jybot
Resource         libdoc_resource.txt
Suite Setup      Run Libdoc And Parse Model From HTML    ${TESTDATADIR}/InternalLinking.py
Test Template    Doc Should Contain Link

*** Test Cases ***
Linking to sections in introduction
    ${MODEL}    Introduction    Introduction
    ${MODEL}    Introduction    Library INTROduction
    ${MODEL}    Importing       importing
    ${MODEL}    Importing       Library Importing
    ${MODEL}    Shortcuts       ShortCuts
    ${MODEL}    Keywords        Keywords

Linking to sections in importing and keywords
    ${MODEL['inits'][0]}       Introduction    introduction
    ${MODEL['keywords'][0]}    Importing       Importing
    ${MODEL['keywords'][1]}    Shortcuts       shortcuts

Linking to keywords in introduction
    ${MODEL}    Keyword           Keyword
    ${MODEL}    Second Keyword    secoNd kEywoRD

Linking to keywords in importing and keywords
    ${MODEL['inits'][0]}       Keyword           keyword
    ${MODEL['keywords'][0]}    Second Keyword    Second Keyword

Non-matching text in backticks gets formatting
    [Template]    Doc Should Contain Name
    ${MODEL}    backticks
    ${MODEL['keywords'][1]}    arg
    ${MODEL['keywords'][1]}    no link

Linking to first level headers in introduction
    [Template]    NONE
    Doc Should Contain Link    ${MODEL}    Linking to headers    linking to headers
    Doc Should Contain Link    ${MODEL}    First = Level =       first = level =
    Doc Should Contain    ${MODEL}    <h2 id="Linking to headers">Linking to headers</h2>
    Doc Should Contain    ${MODEL}    <h2 id="First = Level =">First = Level =</h2>

Linking to first level headers in importing and keywords
    ${MODEL['inits'][0]}       Formatting            formatting
    ${MODEL['keywords'][1]}    Linking to headers    linking to headers

Linking to second and third level headers
    [Template]    NONE
    Doc Should Contain Link    ${MODEL}                   Second level    Second level
    Doc Should Contain Link    ${MODEL}                   Third level     third level
    Doc Should Contain Link    ${MODEL['keywords'][1]}    Second level    Second LEVEL
    Doc Should Contain    ${MODEL}    <h3 id="Second level">Second level</h3>
    Doc Should Contain    ${MODEL}    <h4 id="Third level">Third level</h4>

Only headers in introduction are linkable
    [Template]    NONE
    Doc Should Contain Name    ${MODEL['keywords'][1]}    not linkable
    Doc Should Contain    ${MODEL['keywords'][1]}    <h2>Not linkable</h2>


*** Keywords ***

Doc Should Contain Link
    [Arguments]    ${object}    ${target}    ${text}
    Doc Should Contain   ${object}    <a href="#${target}" class="name">${text}</a>

Doc Should Contain Name
    [Arguments]    ${object}    ${text}
    Doc Should Contain   ${object}    <span class="name">${text}</span>

Doc Should Contain
    [Arguments]    ${object}    ${text}
    Doc Should Contain In HTML    ${object}    ${text}

