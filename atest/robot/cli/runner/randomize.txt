*** Settings ***
Suite Setup     Check That Default Orders Are Correct
Force Tags      pybot  jybot  regression
Resource        cli_resource.txt

*** Variables ***
${DEFAULT SUITE ORDER}  [Suite First, Sub.Suite.1, Suite3, Suite4, Suite5, Suite10, Suite 6, SUite7, suiTe 8, Suite 9 Name]
${DEFAULT TEST ORDER}  [test1, test2, test3, test4, test5, test6, test7, test8, test9, test10, test11, test12]

*** Test Cases ***
Randomizing Tests
    [Setup]  Run Tests  --randomize test  misc/multiple_suites/01__suite_first.html
    Should Not Be Equal As Strings  ${SUITE.tests}  ${DEFAULT TEST ORDER}

Randomizing Suites
    [Setup]  Run Tests  --randomize Suites  misc/multiple_suites
    Should Not Be Equal As Strings  ${SUITE.suites}  ${DEFAULT SUITE ORDER}
    ${tests} =  Get Tests
    Should Be Equal As Strings  ${tests}  ${DEFAULT TEST ORDER}

Randomizing Suites And Tests
    [Setup]  Run Tests  --randomize all  misc/multiple_suites
    Should Not Be Equal As Strings  ${SUITE.suites}  ${DEFAULT SUITE ORDER}
    ${tests} =  Get Tests
    Should Not Be Equal As Strings  ${tests}  ${DEFAULT TEST ORDER}

Randomizing Suites And Tests with seed
    [Setup]  Run Tests  --randomize all:1234  misc/multiple_suites
    ${suites1} =  Get Suites
    ${tests1} =  Get Tests
    Should Not Be Equal As Strings  ${suites1}  ${DEFAULT SUITE ORDER}
    Should Not Be Equal As Strings  ${tests1}  ${DEFAULT TEST ORDER}
    Run Tests  --randomize all:1234  misc/multiple_suites
    ${suites2} =  Get Suites
    ${tests2} =  Get Tests
    Should Be Equal As Strings  ${suites1}	${suites2}
    Should Be Equal As Strings  ${tests1}	${tests2}

Last Option Overrides All Previous
    [Setup]  Run Tests  --randomize suites --randomize tests --randomize none  misc/multiple_suites
    Should Be Equal As Strings  ${SUITE.suites}  ${DEFAULT SUITE ORDER}
    ${tests} =  Get Tests
    Should Be Equal As Strings  ${tests}  ${DEFAULT TEST ORDER}

Invalid Option value
    Run Should Fail  --randomize INVALID ${TESTFILE}  Option '--randomize' does not support value 'INVALID'.

Invalid seed value
    Run Should Fail  --randomize all:test ${TESTFILE}  Option '--randomize' does not support value 'all:test'.

*** Keywords ***
Check That Default Orders Are Correct
    Run Tests  ${EMPTY}  misc/multiple_suites
    Should Be Equal As Strings  ${SUITE.suites}  ${DEFAULT SUITE ORDER}
    Should Be Equal As Strings  ${SUITE.suites[0].tests}  ${DEFAULT TEST ORDER}

Get Suites
    [Return]  ${SUITE.suites}

Get Tests
    Comment  This keyword is needed as there is also one directory suite, which does not contain tests.
    ${tests} =  Set Variable If  '${SUITE.suites[0].name}' == 'Sub.Suite.1'  ${SUITE.suites[0].suites[0].tests}  ${SUITE.suites[0].tests}
    [Return]  ${tests}
