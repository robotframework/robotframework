*** Settings ***
Suite Setup       My Setup
Force Tags        regression
Default Tags      pybot    jybot
Resource          atest_resource.txt

*** Test Cases ***
Statistics Should Be Written to XML
    ${output} =    Get File    ${OUTFILE}
    ${exp} =    Catenate    SEPARATOR=\\r?\\n    (?s)    <statistics>    <total>    .*    </total>
    ...    <tag>    .*    </tag>    <suite>    .*    </suite>    </statistics>
    Should Match Regexp    ${output}    ${exp}

Total statistics should be Correct
    ${stats} =    Get Element    ${OUTFILE}    statistics/total
    ${crit}    ${total} =    Call Method    ${stats}    findall    stat
    Node Should Be Correct    ${crit}    Critical Tests    5    1
    Node Should Be Correct    ${total}    All Tests    9    1

Tag statistics should be Correct
    ${stats} =    Get Element    ${OUTFILE}    statistics/tag
    Tag Node Should Be Correct    ${stats[0]}    t1    5    1    critical
    Tag Node Should Be Correct    ${stats[1]}    d1 AND d2    1    0    combined
    Tag Node Should Be Correct    ${stats[2]}    F1 NOT T_1    4    0    info=combined
    Tag Node Should Be Correct    ${stats[3]}    d1    1    0
    Tag Node Should Be Correct    ${stats[4]}    d2    1    0
    Tag Node Should Be Correct    ${stats[5]}    f1    9    1
    Tag Node Should Be Correct    ${stats[6]}    sub3    2    0
    Tag Node Should Be Correct    ${stats[7]}    XxX    9    1

Suite statistics should be Correct
    ${stats} =    Get Element    ${OUTFILE}    statistics/suite
    Node Should Be Correct    ${stats[0]}    Suites    9    1
    Node Should Be Correct    ${stats[1]}    Suites.Fourth    0    1
    Node Should Be Correct    ${stats[2]}    Suites.Subsuites    2    0
    Node Should Be Correct    ${stats[3]}    Suites.Subsuites2    2    0
    Node Should Be Correct    ${stats[4]}    Suites.Tsuite1    3    0
    Node Should Be Correct    ${stats[5]}    Suites.Tsuite2    1    0
    Node Should Be Correct    ${stats[6]}    Suites.Tsuite3    1    0

*** Keywords ***
My Setup
    Run Tests    ${EMPTY}    misc/suites
    Move File    ${OUTFILE}    ${OUTDIR}/statistics.xml
    ${options} =    Catenate
    ...    --critical t1
    ...    --tagstatcombine d1ANDd2
    ...    --suitestatlevel 2
    ...    --tagstatexclude t2
    ...    --TagStatComb F1NOTT_1
    ...    --SetTag XxX
    Run Rebot    ${options}    ${OUTDIR}/statistics.xml

Node Should Be Correct
    [Arguments]    ${node}    ${name}    ${pass}    ${fail}
    Element Text Should Be      ${node}      ${name}
    Element Attribute Should Be      ${node}      pass      ${pass}
    Element Attribute Should Be      ${node}      fail      ${fail}

Tag Node Should Be Correct
    [Arguments]    ${node}    ${name}    ${pass}    ${fail}    ${info}=
    Node Should Be Correct    ${node}    ${name}    ${pass}    ${fail}
    Element Attribute Should Be      ${node}      info      ${info}
