*** Settings ***
Suite Setup     Run And Rebot Flattened
Force Tags      regression  pybot  jybot
Resource        atest_resource.txt

*** Variables ***
${FLATTEN}      --FlattenKeywords NAME:Keyword3 --flat name:key*others --FLAT name:builtin.* --log log.html
${FLAT TEXT}    _*Keyword content flattened.*_
${FLAT HTML}    <p><i><b>Keyword content flattened.\\x3c/b>\\x3c/i>\\x3c/p>
${ERROR}        [ ERROR ] Invalid value for option '--flattenkeywords'. Expected pattern to start with 'NAME:' but got 'invalid'.${USAGE TIP}\n

*** Test Cases ***
Non-matching keyword is not flattened
    Should Be Equal    ${TC.kws[0].doc}    Doc of keyword 2
    Length Should Be    ${TC.kws[0].kws}    2
    Length Should Be    ${TC.kws[0].msgs}    0
    Check Log Message    ${TC.kws[0].kws[0].msgs[0]}    2
    Check Log Message    ${TC.kws[0].kws[1].kws[0].msgs[0]}    1

Exact match
    Should Be Equal    ${TC.kws[1].doc}    Doc of keyword 3\n\n${FLAT TEXT}
    Length Should Be    ${TC.kws[1].kws}    0
    Length Should Be    ${TC.kws[1].msgs}    3
    Check Log Message    ${TC.kws[1].msgs[0]}    3
    Check Log Message    ${TC.kws[1].msgs[1]}    2
    Check Log Message    ${TC.kws[1].msgs[2]}    1

Pattern match
    Should Be Equal    ${TC.kws[2].doc}    ${FLAT TEXT}
    Length Should Be    ${TC.kws[2].kws}    0
    Length Should Be    ${TC.kws[2].msgs}    6
    Check Log Message    ${TC.kws[2].msgs[0]}    3
    Check Log Message    ${TC.kws[2].msgs[1]}    2
    Check Log Message    ${TC.kws[2].msgs[2]}    1
    Check Log Message    ${TC.kws[2].msgs[3]}    2
    Check Log Message    ${TC.kws[2].msgs[4]}    1
    Check Log Message    ${TC.kws[2].msgs[5]}    1

Match full name
    Should Be Equal    ${TC.kws[3].doc}    Logs the given message with the given level.\n\n${FLAT TEXT}
    Length Should Be    ${TC.kws[3].kws}    0
    Length Should Be    ${TC.kws[3].msgs}    1
    Check Log Message    ${TC.kws[3].msgs[0]}    Flatten me too!!

Flattened in log after execution
    Should Contain X Times    ${LOG}    Doc of keyword 3    1
    Should Contain X Times    ${LOG}    Doc of keyword 2    1
    Should Contain X Times    ${LOG}    Doc of keyword 1    1
    Should Contain X Times    ${LOG}    Keyword content flattened    3
    Should Contain    ${LOG}    *<p>Doc of keyword 3\\x3c/p>\\n${FLAT HTML}
    Should Contain    ${LOG}    *${FLAT HTML}
    Should Contain    ${LOG}    *<p>Logs the given message with the given level.\\x3c/p>\\n${FLAT HTML}

Invalid usage
    Run Rebot Without Processing Output    ${FLATTEN} --FlattenKeywords invalid   ${OUTFILE}
    Stderr Should Be Equal To    ${ERROR}
    Run Tests Without Processing Output    ${FLATTEN} --FlattenKeywords invalid   output/flatten_keywords.txt
    Stderr Should Be Equal To    ${ERROR}

*** Keywords ***
Run And Rebot Flattened
    Run Tests Without Processing Output     ${FLATTEN}    output/flatten_keywords.txt
    ${LOG} =    Get File    ${OUTDIR}/log.html
    Set Suite Variable    $LOG
    Run Rebot    ${FLATTEN}    ${OUTFILE}
    ${TC} =    Check Test Case    Flatten stuff
    Set Suite Variable    $TC
